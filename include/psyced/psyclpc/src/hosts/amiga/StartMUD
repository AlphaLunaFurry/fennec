.key OPT/F

;*************************************
;*                                   *
;*  StartMUD - ldmud startup script  *
;*     (C)2001 Mariko@Wunderland     *
;*                                   *
;*    change whatever you like :)    *
;*                                   *
;*************************************

FailAt 21

Version >NIL: bsdsocket.library
IF FAIL
 Echo Please start your TCP/IP stack first!
 Quit 20
ENDIF

Assign >NIL: mud: EXISTS
IF WARN
 Assign mud: ""
 Set §ldmud§mud§remove 1
ENDIF

Assign >NIL: tmp: EXISTS
IF WARN
 Assign tmp: mud:tmp
 Set §ldmud§tmp§remove 1
ENDIF
Delete >NIL: tmp:_ldmud.swap#? QUIET FORCE

CPU >NIL: CHECK 68040
IF NOT WARN
 Set §ldmud§cpu 040
 Skip GOON
ENDIF
CPU >NIL: CHECK 68030
IF NOT WARN
 CPU >NIL: CHECK 68881
 IF NOT WARN
  Set §ldmud§cpu 030881
  Skip GOON
 ENDIF
ENDIF
CPU >NIL: CHECK 68020
IF NOT WARN
 Set §ldmud§cpu 020
 Skip GOON
ENDIF
Set §ldmud§cpu 000
Lab GOON

IF EXISTS mud:lib/secure/master.c
 Set §ldmud§opt -m /mud/lib -M /secure/master
ELSE
 IF EXISTS mud:lib/test_master.c
  Set §ldmud§opt -m /mud/lib -M /test_master
 ELSE
  Set §ldmud§opt ""
 ENDIF
ENDIF

Stack 262144
mud:bin/driver.$§ldmud§cpu $§ldmud§opt <OPT>
Stack 4096

Unset §ldmud§opt
Unset §ldmud§cpu

Delete >NIL: tmp:_ldmud.swap#? QUIET FORCE
IF $§ldmud§tmp§remove EQ 1
 Assign tmp:
 Unset §ldmud§tmp§remove
ENDIF

IF $§ldmud§mud§remove EQ 1
 Assign mud:
 Unset §ldmud§mud§remove
ENDIF
